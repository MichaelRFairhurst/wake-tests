#>
# Source Code for the Original Compiler for the
# Programming Language Wake
#
# OptionalTypeTest.wk
#
# Licensed under the MIT license
# See LICENSE.TXT for details
#
# Author: Michael Fairhurst
# Revised By:
#
##
module wktest;

import wunit.Asserts;

@TestClass
every OptionalTypeTest is:

	@Test
	~[test optional int never set]~(Asserts) {
		var Int? = nothing;
		if Int exists {
			Asserts.fail("Int doesn't exist");
		}
	}

	@Test
	~[test optional int set]~(Asserts) {
		var Int? = 5;
		if Int exists {
			Asserts.that(Int)Equals(5);
			return;
		}
		Asserts.fail("Int should have existed");
	}

	@Test
	~[test optional int not set will hit else]~(Asserts) {
		var Int? = nothing;
		if Int exists {} else {
			return;
		}
		Asserts.fail("We didn't hit the else block");
	}

	@Test
	~[test optional int set wont hit else]~(Asserts) {
		var Int? = 5;
		if Int exists {} else {
			Asserts.fail("Should not have hit else block");
		}

	}

	@Test
	~[test zero and empty string are not nothing]~(Asserts) {
		var Int? = 0;
		var Text? = "";
		if Int exists { } else { Asserts.fail("0 is read as nothing"); }
		if Text exists { } else { Asserts.fail("\"\" is read as nothing"); }
		Asserts.that(Int != nothing);
		Asserts.that(Text != nothing);
	}
